#!/usr/bin/perl -w
use strict;

=head1 NAME

generate_makefile_segments - build some NMS v2/buildtools/*.mak files

=head1 DESCRIPTION

This script holds a list of all the source files in the v2
subdirectory of the NMS CVS tree that can effect package contents,
and builds a couple of Makefile segments with targets for each
source file.

The first is F</v2/buildtools/ChangeLog.mak>, which holds targets
for updating a local cache of the CVS log for each source file.  A
target for building F<.pkg/ChangeLog> is also defined.

The second is F</v2/buildtools/libfiles.mak>, which holds targets
for copying C<.pm> files from the main source tree into the
F<.pkg/lib> directory of C<*_modules> packages.

Both of these makefile segements are intended to be included from
F</v2/packages/*/Makefile>.

=cut

my @files = qw(

  src/formmail/FormMail.pl
  src/formmail/README
  src/formmail/EXAMPLES

  lib/CGI/NMS/Charset.pm
  lib/CGI/NMS/Validator.pm
  lib/CGI/NMS/Mailer.pm
  lib/CGI/NMS/Mailer/ByScheme.pm
  lib/CGI/NMS/Mailer/Sendmail.pm
  lib/CGI/NMS/Mailer/SMTP.pm
  lib/CGI/NMS/Script.pm
  lib/CGI/NMS/Script/FormMail.pm

  package/formmail_modules/Makefile
  package/formmail_modules/README.install
  package/formmail_modules/README.manifest

  package/formmail_compat/Makefile
  package/formmail_compat/README.install
  package/formmail_compat/README.manifest

  buildtools/generate_makefile_segments
  buildtools/tarballs
  buildtools/inline
  buildtools/cvs2cl

);

unlink 'ChangeLog.mak'; # becuase we set it readonly
open MAKEFILE, ">ChangeLog.mak" or die "open: $!";
print MAKEFILE <<'END';
#
# This Makefile segment defines dependencies for keeping individual
# CVS log files of all source files in date on the local system,
# and for merging subsets of those files into changelogs for
# particular packages.
#
# This CVS log cache system is intended to allow changelogs to be
# built from the CVS log entries, without going to sourceforge for
# the CVS log for each source file more than once per source file
# change.
#
# Include this Makefile into a package Makefile to define the
# dependencies of the .pkg/ChangeLog file.
#
# Imports from environment:
#
#   NMS_WORKING_COPY: The full filesystem path to the root of a
#                     working copy against the NMS CVS tree.
#
# Imports from parent Makefile:
#
#   SOURCE_FILES:     A list of the source files who's CVS change
#                     logs should be merged into .pkg/ChangeLog,
#                     relative to the v2 subdirectory of the CVS
#                     tree.
#
# Autogenerated by /v2/buildtools/generate_makefile_segments
#
#    *****  DO NOT EDIT THIS FILE BY HAND  *****
#

CHANGELOG_BASE = $(NMS_WORKING_COPY)/v2/.changelog

CHANGELOG_FILES = $(SOURCE_FILES:%=$(CHANGELOG_BASE)/%)

.pkg/ChangeLog: .pkg $(CHANGELOG_FILES)
	(cd $(CHANGELOG_BASE) && ../buildtools/cvs2cl_cat --revisions --stdout $(SOURCE_FILES)) >$@

END

foreach my $file (@files) {
    my $dir = $file;
    $dir =~ s#/[^/]+$##;

    print MAKEFILE <<END;

\$(CHANGELOG_BASE)/$file: \$(NMS_WORKING_COPY)/v2/$file
\tmkdir -p \$(NMS_WORKING_COPY)/v2/.changelog/$dir
\tcd \$(NMS_WORKING_COPY) && cvs log v2/$file >\$\@
\ttest -s \$\@ || (rm \$\@ ; false)
END

}

close MAKEFILE or die "close ChangeLog.mak: $!";
chmod 0444, 'ChangeLog.mak' or die "chmod ChangeLog.mak: $!";


unlink 'libfiles.mak'; # becuase we set it readonly
open MAKEFILE, ">libfiles.mak" or die "open libfiles.mak: $!";
print MAKEFILE <<'END';
#
# Makefile component for a package with the modules supplied as 
# individual files to be uploaded by hand.
#
# Defines targets to copy each C<.pm> file under F</v2/lib> into
# a lib directory under F<.pkg>, creating any missing directories.
# 
# Autogenerated by /v2/buildtools/generate_makefile_segments
#
#    *****  DO NOT EDIT THIS FILE BY HAND  *****
#
END

foreach my $lib (grep m#^lib/#, @files) {
    print MAKEFILE <<END;

.pkg/$lib: .pkg ../../$lib
	(cd ../.. && tar cf - '$lib') | (cd .pkg && tar xf -)
END
}

close MAKEFILE or die "close libfiles.mak: $!";
chmod 0444, 'libfiles.mak' or die "chmod libfiles.mak: $!";

